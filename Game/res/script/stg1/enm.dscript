
// Accelerating enemy, fires a ring of aimed bullets
task circleEnm(x, turnDir){
    
    set e = enemy(_fighter, x, _top - 32, 100);
    returnif e.isDeleted();
    
    e.setDir(90);
    e.setSpd(1);
    
    wait 40;
    
    e.setMaxSpd(6);
    e.setAccel(0.04);
    
    if(turnDir == 1)
        e.setAngVel(0.2);
    else if(turnDir == 2)
        e.setAngVel(-0.2);
    
    wait 15;
    
    set const c = 16;
    
    for(i < 2){
        set dir = angleToPlayer(e.getPos()) + rand(30);
        
        for(d < 0, 360/c, 360)
            bullet(_orb, (i -> _blue, _dark_blue), e.getPos(), dir + d, 2.5, 10);
        
        wait 60;
    }
}

// Fires aimed barrage
task aimEnm(x, type){
    
    set e = enemy(_fighter, x, _top - 32, 120);
    returnif e.isDeleted();
    
    set spd = randf(3, 5);
    
    e.setDir(90);
    e.setSpd(spd);
    e.setMinSpd(0);
    e.setAccel(-spd/60);
    
    set av = randBool() -> 0.4, -0.4;
    e.setAngVel(av);
    
    wait 60;
    
    e.setAngVel(0);
    
    set ang = type -> 80, 60;
    set dir = angleToPlayer(e.getPos());
    
    set col1 = type -> _green, _pink;
    set col2 = type -> _cyan, _purple;
    
    for(i < 3){
        set c = i%2 == 0 -> 7, 6;
        if(type == 1) c -= 2;
        
        for(d <= -ang/2, ang/(c - 1), ang/2)
            bullet(_crystal, (i%2 == 0 -> col1, col2), e.getPos(), dir + d, 2 + i/2, 10);
    }
    
    e.setDir(e.getDir() + 180);
    e.setMaxSpd(4);
    e.setAccel(spd/60);
    e.setAngVel(-av);
}

// High HP enemy, spiral pattern
task tankEnm(x, inv){
    
    set e = enemy(_fighter, x, _top - 32, 2500);
    returnif e.isDeleted();
    
    e.setDir(90);
    e.setSpd(6);
    e.setAccel(-0.08);
    e.setMinSpd(0);
    
    set dir = 90;
    set dir2 = 45;
    set dir3 = 180;
    
    for(i < 84){
        
        // Slow red spiral
        for(d < 0, 360/4, 360)
            bullet(_rice, (inv -> _cyan, _red), e.getPos() + radius(dir + d, 10), dir + d, 1.5, -10);
        
        // Fast blue spiral
        for(d < 0, 360/8, 360)
            bullet(_rice, (inv -> _purple, _light_blue), e.getPos() + radius(dir2 + d, 25), dir2 + d, 2.2, -10);
        
        // Large bullets
        if(i%4 == 0){
            for(d < 0, 360/6, 360){
                set b = bullet(_orb, (inv -> _yellow, _purple), e.getPos() + radius(dir3 + d, 30), dir3 + d, 1, 10);
                b.setScale(1.4);
                b.setHitboxScale(1.8);
            }
        }
        
        dir +=  inv -> -4.3, 4.3;
        dir2 += inv -> 9.7, -9.7;
        dir3 += inv -> -71, 71;
        
        wait 5;
    }
    
    e.setAccel(0.05);
    e.setMaxSpd(1.5);
}

// Mid HP enemy, moves down slowly, spiral pattern
task spiralEnm(x, inv){
    
    set e = enemy(_fighter, x, _top - 32, 400);
    returnif e.isDeleted();
    
    e.setDir(90);
    e.setSpd(4);
    e.setAccel(-0.08);
    e.setMinSpd(0.5);
    
    set dir = 90;
    set dirAdd = inv -> 6.3, -6.3;
    
    for(i < 84){
        
        for(d < 0, 360/4, 360){
            set b = bullet(_crystal, _red, e.getPos() + radius(dir + d, 5), dir + d, 4, 2, 4, -0.08, -10);
            b.setAdditive();
        }
        
        dir += dirAdd;
        
        wait 5;
    }
    
    e.setAccel(0.05);
    e.setMaxSpd(4);
}

// Enemy moving in an S path
task sEnm(aimed, inv){
    
    set e = enemy(_radial, (inv -> _right + 20, _left - 20), _top + 50, 120);
    returnif e.isDeleted();
    fire(aimed, e);
    
    e.setDir(inv -> 180, 0);
    e.setSpd(6);
    
    wait 19;
    
    e.setAngVel(inv -> -3, 3);
    wait 20;
    
    e.setAngVel(inv -> 3, -3);
    wait 20;
    
    e.setAngVel(0);
    
    task fire(aimed, e){
        returnif e.isDeleted();
        
        set const c = 8;
        set const c2 = 16;
        
        set w = rand(10, 40);
        wait w;
        
        while(true){
            set dir = angleToPlayer(e.getPos());
            set dir2 = rand(0, 360);
            
            if(aimed){
                for(d < 0, 360/c, 360){
                    for(s <= 1, 0.8, 4)
                        bullet(_crystal, _light_blue, e.getPos() + radius(dir + d, 10), dir + d, s*3, s, s*3, -0.6, 5);
                }
            }
            
            else{
                for(d < 0, 360/c2, 360)
                    bullet(_scale, randChoice({_light_blue, _blue, _dark_blue}), e.getPos() + radius(dir2 + d, 30), dir2 + d + rand(10), 8, randf(1, 2.5), 8, -0.3, 8);
            }
            
            wait aimed -> 60, 40;
        }
    }
}

// Falling, accelerating weak enemies
task fallEnm(){
    
    set e = enemy(_radial, rand(_left + 30, _right - 30), _top - 20, 120);
    returnif e.isDeleted();
    
    e.setDir(90 + rand(10));
    e.setSpd(2);
    
    wait 30;
    
    e.setMaxSpd(5);
    e.setAccel(0.02);
    
    set const c = 12;
    set dir = rand(0, 360);
    
    for(i < 2){
        
        set off = randBool() -> -90, 90;
        
        for(d < 0, 360/c, 360)
            bullet(_crystal, randChoice({_purple, _pink}), e.getPos() + radius(dir + d, 20), dir + d + off, 2, 8);
        
        wait 40;
    }
}